apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'realm-android'
apply plugin: 'io.fabric'

def static gitVersionCode() {
    def cmd = 'git rev-list HEAD --first-parent --count'
    cmd.execute().text.trim().toInteger()
}

def static gitVersionTag() {
    def cmd = 'git describe --tags'
    def version = cmd.execute().text.trim()

    def pattern = "-(\\d+)-g"
    def matcher = version =~ pattern

    if (matcher) {
        version = version.substring(0, matcher.start()) + "." + matcher[0][1]
    } else {
        version = version + ".0"
    }

    return version
}


android {

    signingConfigs {
        config {
            keyAlias rootProject.keyAlias
            keyPassword rootProject.keyPassword
            storeFile file('keystore/awaker.jks')
            storePassword rootProject.storePassword
        }
    }

    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        applicationId "com.future.awaker"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion

        versionCode 1
        versionName "1.0"
    }

    buildTypes {

        debug {
            // 为了不和 release 版本冲突
            applicationIdSuffix ".debug"

            manifestPlaceholders = [
                    fabric_apikey: rootProject.fabric_apikey
            ]
        }

        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config

            manifestPlaceholders = [
                    fabric_apikey: rootProject.fabric_apikey
            ]
        }
    }

    dataBinding {
        enabled = true
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    applicationVariants.all { variant ->
        if (variant.buildType.name.equals('release')) {
            variant.mergedFlavor.versionCode = gitVersionCode()
            variant.mergedFlavor.versionName = gitVersionTag()
        }

        variant.outputs.each { output ->
            def fileName = output.outputFile.name;
            fileName = fileName.replace("app", "awaker-${variant.versionName}-${variant.versionCode}")
            output.outputFile = new File(output.outputFile.parent, fileName)
        }
    }
}

dependencies {
    def supportLibraryVersion = rootProject.supportLibraryVersion
    def retrofitVersion = '2.3.0'
    def glideVersion = '4.0.0'

    compile fileTree(include: ['*.jar'], dir: 'libs')

    compile "com.android.support:appcompat-v7:$supportLibraryVersion"
    compile "com.android.support:cardview-v7:$supportLibraryVersion"
    compile "com.android.support:design:$supportLibraryVersion"
    compile "com.android.support:recyclerview-v7:$supportLibraryVersion"

    //retrofit
    compile "com.squareup.retrofit2:retrofit:$retrofitVersion"
    compile "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    compile "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    compile 'com.squareup.okhttp3:logging-interceptor:3.4.1'

    //rx
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'io.reactivex.rxjava2:rxjava:2.1.0'

    //glide
    compile "com.github.bumptech.glide:glide:$glideVersion"
    annotationProcessor "com.github.bumptech.glide:compiler:$glideVersion"

    // core ui
    compile 'com.gordonwong:material-sheet-fab:1.2.1'

    compile 'com.pnikosis:materialish-progress:1.1'
    compile 'net.soulwolf.widget:ratiolayout:1.0.0'
    compile 'com.github.chrisbanes:PhotoView:2.0.0'
    compile 'com.just.agentweb:agentweb:1.2.6'
    compile 'org.jsoup:jsoup:1.10.3'
    compile 'com.allenliu.versionchecklib:library:1.5'
    compile 'com.afollestad.material-dialogs:core:0.9.4.6'

    compile 'com.sun.mail:android-mail:1.5.5'
    compile 'com.sun.mail:android-activation:1.5.5'

    compile 'com.blankj:utilcode:1.8.2'

    compile('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true;
    }
}
